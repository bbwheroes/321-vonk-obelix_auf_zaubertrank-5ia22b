plugins {
    id 'java-library'
    id 'org.springframework.boot'
    id 'org.asciidoctor.jvm.convert' version '4.0.2'
}

ext {
    snippetsDir = file("$buildDir/generated-snippets")
    asciidocOutDir = file("$buildDir/docs/asciidoc")
}

configurations {
    asciidoctorExt
}

dependencies {
    implementation project(':obelix-common')
    implementation project(':obelix-quarry-api')
    implementation project(':obelix-basket-api')
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0"

    testImplementation testFixtures(project(':obelix-common'))
    testImplementation 'org.springframework.restdocs:spring-restdocs-webtestclient'

    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
}

tasks.register('restdocsTest', Test) {
    useJUnitPlatform()
    outputs.dir snippetsDir
    include '**/*ApiDocumentationTest*'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('asciidoctor') {
    inputs.dir snippetsDir
    configurations 'asciidoctorExt'
    attributes 'snippets': snippetsDir
    dependsOn tasks.named('restdocsTest')
    outputDir = asciidocOutDir
    baseDirFollowsSourceFile()
}

tasks.named('bootJar') {
    dependsOn tasks.named('asciidoctor')
    from(asciidocOutDir) {
        into 'static/docs'
    }
}

tasks.named('bootRun') {
    // mainClass = 'ch.bbw.m321.quarry.QuarryApplication'
}
