import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.6' apply false
	id 'io.spring.dependency-management' version '1.1.7'
}

subprojects {
	apply plugin: 'java'

	group = 'ch.bbw.m321'
	version = '0.0.1-SNAPSHOT'
	description = 'Obelix and Co. Webshop'

	repositories {
		mavenCentral()
	}

	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(21)
		}
	}

	apply plugin: 'io.spring.dependency-management'
	dependencyManagement {
		imports {
			mavenBom SpringBootPlugin.BOM_COORDINATES
		}
	}

	configurations {
		annotationProcessor {}
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	dependencies {
		annotationProcessor 'org.projectlombok:lombok'
		compileOnly 'org.projectlombok:lombok'
	}

	tasks.named('compileJava') {
		options.compilerArgs << '-parameters'
	}

	tasks.named('test') {
		jvmArgs "-XX:+EnableDynamicAgentLoading"
		useJUnitPlatform()
	}
}
